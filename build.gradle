/****************************************************************
 * Gradle Scipt for Building Microsoft JDBC Driver for SQL Server
 ****************************************************************
 * Instruction for Building JDBC Driver:
 * For building particular version of the driver, use commands:
 * jre13 - - PS> gradle build
			 PS> gradle build -PbuildProfile=jre13
 * jre11 - - PS> gradle build -PbuildProfile=jre11
 * jre8  - - PS> gradle build -PbuildProfile=jre8
 *
 * For Excluding Groups in command line:
 * PS> gradle build -PbuildProfile=jre11 "-PexcludedGroups=['xSQLv15','xGradle']"
 ****************************************************************/

apply plugin: 'java'

version = '8.2.1'
def jreVersion = ""
def testOutputDir = file("build/classes/java/test")
def archivesBaseName = 'mssql-jdbc'
def excludedFile = 'com/microsoft/sqlserver/jdbc/SQLServerJdbc42.java'

allprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Test) {
       systemProperty "file.encoding", "UTF-8"
    }
}

test {
    useJUnitPlatform {
        excludeTags (hasProperty('excludedGroups') ? excludedGroups : 'xSQLv15','xGradle','reqExternalSetup','NTLM')
    }
}

if (!hasProperty('buildProfile') || (hasProperty('buildProfile') && buildProfile == "jre13")){
	
	jreVersion = "jre13"
	excludedFile = 'com/microsoft/sqlserver/jdbc/SQLServerJdbc42.java'
	jar {
		manifest {
			attributes 'Automatic-Module-Name': 'com.microsoft.sqlserver.jdbc'
		}
	}
	sourceCompatibility = 13
	targetCompatibility = 13
}

if (hasProperty('buildProfile') && buildProfile == "jre11"){
	
	jreVersion = "jre11"
	excludedFile = 'com/microsoft/sqlserver/jdbc/SQLServerJdbc42.java'
	jar {
		manifest {
			attributes 'Automatic-Module-Name': 'com.microsoft.sqlserver.jdbc'
		}
	}
	sourceCompatibility = 11
	targetCompatibility = 11
}

if(hasProperty('buildProfile') && buildProfile == "jre8") {
	
	jreVersion = "jre8"
	excludedFile = 'com/microsoft/sqlserver/jdbc/SQLServerJdbc43.java'
	
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	test {
		useJUnitPlatform {
			excludeTags (hasProperty('excludedGroups') ? excludedGroups : 'xSQLv15','xGradle','NTLM','reqExternalSetup','xJDBC42')
		}
	}
}

jar.archiveName = "${archivesBaseName}-${version}.${jreVersion}.jar"
jar {
	manifest {
        attributes 'Title': "Microsoft JDBC Driver ${version} for SQL Server",
        'Version': version,
        'Vendor': 'Microsoft Corporation'
	}
}
sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
			exclude excludedFile
        }
		resources {
			srcDirs "$projectDir"
			include 'META-INF/services/java.sql.Driver'
		}
    }
	test {
		resources {
			srcDirs "src/test/resources", "AE_Certificates"
			output.resourcesDir = testOutputDir
		}
	}
}

//Get dependencies from Maven central repository
repositories {
    mavenCentral()
}

dependencies {
	compile 'org.osgi:org.osgi.core:6.0.0',
			'org.osgi:org.osgi.compendium:5.0.0'
	compileOnly 'com.microsoft.azure:azure-keyvault:1.2.2',
			'com.microsoft.azure:azure-keyvault-webkey:1.2.1',
			'com.microsoft.rest:client-runtime:1.6.15',
			'com.microsoft.azure:adal4j:1.6.4',
			'org.antlr:antlr4-runtime:4.7.2'
	testCompile 'org.junit.platform:junit-platform-console:1.5.2',
			'org.junit.platform:junit-platform-commons:1.5.2',
			'org.junit.platform:junit-platform-engine:1.5.2',
			'org.junit.platform:junit-platform-launcher:1.5.2',
			'org.junit.platform:junit-platform-runner:1.5.2',
			'org.junit.platform:junit-platform-surefire-provider:1.3.2',
			'org.junit.jupiter:junit-jupiter-api:5.5.2',
			'org.junit.jupiter:junit-jupiter-engine:5.5.2',
			'com.zaxxer:HikariCP:3.4.1',
			'org.apache.commons:commons-dbcp2:2.6.0',
			'org.slf4j:slf4j-nop:1.7.29',
			'org.antlr:antlr4-runtime:4.7.2',
			'org.eclipse.gemini.blueprint:gemini-blueprint-mock:2.1.0.RELEASE'
}
