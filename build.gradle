/****************************************************************
 * Gradle Scipt for Building Microsoft JDBC Driver for SQL Server
 ****************************************************************
 * Instruction for Building JDBC Driver:
 * For building particular version of the driver, use commands:
 * jre15 - - PS> gradle build
			 PS> gradle build -PbuildProfile=jre15
 * jre11 - - PS> gradle build -PbuildProfile=jre11
 * jre8  - - PS> gradle build -PbuildProfile=jre8
 *
 * For Excluding Groups in command line:
 * PS> gradle build -PbuildProfile=jre11 "-PexcludedGroups=['xSQLv15','xGradle']"
 ****************************************************************/

apply plugin: 'java'

version = '9.1.0'
def jreVersion = ""
def testOutputDir = file("build/classes/java/test")
def archivesBaseName = 'mssql-jdbc'
def excludedFile = 'com/microsoft/sqlserver/jdbc/SQLServerJdbc42.java'

allprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Test) {
       systemProperty "file.encoding", "UTF-8"
    }
}

test {
    useJUnitPlatform {
        excludeTags (hasProperty('excludedGroups') ? excludedGroups : 'xSQLv15','xGradle','reqExternalSetup','NTLM','MSI','clientCertAuth','fedAuth')
    }
}

if (!hasProperty('buildProfile') || (hasProperty('buildProfile') && buildProfile == "jre15")){
	
	jreVersion = "jre15"
	excludedFile = 'com/microsoft/sqlserver/jdbc/SQLServerJdbc42.java'
	jar {
		manifest {
			attributes 'Automatic-Module-Name': 'com.microsoft.sqlserver.jdbc'
		}
	}
	sourceCompatibility = 15
	targetCompatibility = 15
}

if (hasProperty('buildProfile') && buildProfile == "jre11"){
	
	jreVersion = "jre11"
	excludedFile = 'com/microsoft/sqlserver/jdbc/SQLServerJdbc42.java'
	jar {
		manifest {
			attributes 'Automatic-Module-Name': 'com.microsoft.sqlserver.jdbc'
		}
	}
	sourceCompatibility = 11
	targetCompatibility = 11
}

if(hasProperty('buildProfile') && buildProfile == "jre8") {
	
	jreVersion = "jre8"
	excludedFile = 'com/microsoft/sqlserver/jdbc/SQLServerJdbc43.java'
	
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	test {
		useJUnitPlatform {
			excludeTags (hasProperty('excludedGroups') ? excludedGroups : 'xSQLv15','xGradle','NTLM','reqExternalSetup','MSI','clientCertAuth','fedAuth','xJDBC42')
		}
	}
}

jar.archiveFileName = "${archivesBaseName}-${version}.${jreVersion}-preview.jar"
jar {
	manifest {
        attributes 'Title': "Microsoft JDBC Driver ${version} for SQL Server",
        'Version': version,
        'Vendor': 'Microsoft Corporation'
	}
}
sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
			exclude excludedFile
        }
		resources {
			srcDirs "$projectDir"
			include 'META-INF/services/java.sql.Driver'
		}
    }
	test {
		resources {
			srcDirs "src/test/resources", "AE_Certificates"
			output.resourcesDir = testOutputDir
		}
	}
}

//Get dependencies from Maven central repository
repositories {
    mavenCentral()
}

dependencies {
	compile 'org.osgi:org.osgi.core:6.0.0',
			'org.osgi:org.osgi.compendium:5.0.0'
	compileOnly 'com.azure:azure-security-keyvault-keys:4.2.1',
			'com.azure:azure-identity:1.1.3',
			'org.antlr:antlr4-runtime:4.7.2',
			'com.google.code.gson:gson:2.8.6',
			'org.bouncycastle:bcprov-jdk15on:1.65',
			'org.bouncycastle:bcpkix-jdk15on:1.65'
	testCompile 'org.junit.platform:junit-platform-console:1.5.2',
			'org.junit.platform:junit-platform-commons:1.5.2',
			'org.junit.platform:junit-platform-engine:1.5.2',
			'org.junit.platform:junit-platform-launcher:1.5.2',
			'org.junit.platform:junit-platform-runner:1.5.2',
			'org.junit.platform:junit-platform-surefire-provider:1.3.2',
			'org.junit.jupiter:junit-jupiter-api:5.6.0',
			'org.junit.jupiter:junit-jupiter-engine:5.6.0',
			'org.junit.jupiter:junit-jupiter-params:5.6.0',
			'com.zaxxer:HikariCP:3.4.2',
			'org.apache.commons:commons-dbcp2:2.7.0',
			'org.slf4j:slf4j-nop:1.7.29',
			'org.antlr:antlr4-runtime:4.7.2',
			'org.eclipse.gemini.blueprint:gemini-blueprint-mock:2.1.0.RELEASE',
			'com.google.code.gson:gson:2.8.6',
			'org.bouncycastle:bcprov-jdk15on:1.65',
			'com.azure:azure-security-keyvault-keys:4.2.1',
			'com.azure:azure-identity:1.1.3',
			'com.microsoft.azure:adal4j:1.6.5',
			'com.h2database:h2:1.4.200'
}
